buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.5'
    }
}
plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

def getTagNameFromGit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


group 'moe.banana'
version getTagNameFromGit()

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.squareup.moshi:moshi:1.4.0'
}
sourceSets {
    test {
        resources {
            srcDir "test/resources"
            includes["**/*.json"]
        }
    }
}

repositories {
    jcenter()
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/kamikat/moshi-jsonapi/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

//publish {
//    groupId = 'moe.banana'
//    artifactId = 'moshi-jsonapi'
//    publishVersion = getTagNameFromGit()
//    desc = 'Implement JSON API v1.0 Specification in Moshi.'
//    website = 'https://github.com/kamikat/moshi-jsonapi'
//}
